apply plugin: 'com.android.library'

def jniLibsDir = System.properties['jniLibsDir']
def buildDir = System.properties['buildDir']
def ortVersion = System.properties['ortVersion']

project.buildDir = buildDir
project.version = ortVersion

buildscript {
	repositories {
		google()
		jcenter()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:3.5.3'

		// NOTE: Do not place your application dependencies here; they belong
		// in the individual module build.gradle files
	}
}

allprojects {
	repositories {
		google()
		jcenter()
	}
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.2"

	defaultConfig {
		minSdkVersion 24
		targetSdkVersion 28
		versionCode 1
		versionName = project.version

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

	}

	android {
		lintOptions {
			abortOnError false
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}

	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	sourceSets {
		main {
			jniLibs.srcDirs = [jniLibsDir]
		}
	}

	libraryVariants.all { variant ->
		variant.outputs.each { output ->
			output.outputFileName = "${archivesBaseName}-${variant.name}-${defaultConfig.versionName}.aar"
		}
	}
}

task allJar(type: Jar) {
	manifest {
		attributes('Implementation-Version': project.version)
	}
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
	compile fileTree(dir: 'libs', include: ['*.jar'])

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
	testImplementation 'com.google.protobuf:protobuf-java:3.10.0'
}
